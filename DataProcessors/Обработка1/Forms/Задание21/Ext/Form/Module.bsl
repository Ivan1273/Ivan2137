
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СостояниеПроцесса = "Начало";
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимостьДоступность()
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ИмяТаблицыФормы

&НаКлиенте
Процедура СписокВопросовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	// При выборе вопроса заполняем реквизит формы СписокОтветов и выводим на форму
	ЗаполнитьОтветыВопроса(ВыбраннаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура СписокОтветовВопросаПометкаПриИзменении(Элемент)
	// при выборе ответа нужно проверить параметр МножественныйВыбор
	ПараметрыВопроса = Модель.НайтиПоИдентификатору(ТекущийВопросИндентификатор);
	
	Если Не ПараметрыВопроса.МножественныйВыбор Тогда
	// Если МНожественныйВыбор Ложь то можно выбрать только один ответ и снять другие пометки
		ТекущаяСтрока = Элементы.СписокОтветовВопроса.ТекущаяСтрока;
		ЭлементСписка = СписокОтветовВопроса.НайтиПоИдентификатору(ТекущаяСтрока); // по номеру строки в таблице формы находим ответ
		
		Если ЭлементСписка.Пометка Тогда
		// если пометку установили в Истину то убираем все галочки
			СписокОтветовВопроса.ЗаполнитьПометки(Ложь);
			// устанавливаем текущую пометку в Истина
			ЭлементСписка.Пометка = Истина;
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НачатьТестирование(Команда)
	СостояниеПроцесса = "НачатьТестирование";
	УстановитьВидимостьДоступность();
	
	// Далее нужно заполнить модель.
	ЧтениеJsonЧерезОбъектЧтениеДанныхНаСервере();
	
	// Далее вопросы из модели нужно вывести на форму, то есть заполнить реквизит формы СписокВопросов
	Для каждого СтрокаМодели Из Модель.ПолучитьЭлементы() Цикл
		СписокВопросов.Добавить(СтрокаМодели.ПолучитьИдентификатор(), СтрокаМодели.Текст);
	КонецЦикла;
	
	// Активируем первый вопрос
	ЗаполнитьОтветыВопроса(СписокВопросов[0].ПолучитьИдентификатор());
	// устанавливаем активным поле выбора ответов
	ЭтаФорма.ТекущийЭлемент = Элементы.СписокОтветовВопроса;
КонецПроцедуры

&НаКлиенте
Асинх Процедура Ответить(Команда)
	// Сделаем апгрейд текущей процедуры с использованием асинхронной функции ВопросАСИНХ
	
	// фиксируем ответ пользователя в модели
	// заполняем ОтветВыборПользователя 
	// считываем помеченные элементы списка в массив, содержащий индексэлемента
	// стыковка с моделью будет происходить по индексу
	
	
	// считываем помеченные элементы списка в массив, содержащий индексэлемента
	МассивИндексовОтветов = Новый Массив;
	ИндексОтвета = 0;
	
	// Если ответ выбран то добавляем в массив  его индекс
	Для каждого ЭлементСписка Из СписокОтветовВопроса Цикл
		Если ЭлементСписка.Пометка Тогда
			МассивИндексовОтветов.Добавить(ИндексОтвета);
		КонецЕсли;
		ИндексОтвета = ИндексОтвета + 1;
	КонецЦикла; 
	
	// Если массив пустой, то пользователь не выбрал ответ.
	// Добавим ДиалогВопрос выбора ответа пользователем
	Если Не МассивИндексовОтветов.Количество() Тогда
		ТекстВопроса = "Вы не выбрали вопрос(ы), продолжить?";
		// спрашиваем и ждём ответа
		РезультатВопроса = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
		
		Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
	
	КонецЕсли;
	
	// Получаем из модели вопрос на который отвечаем
	СтрокаВопроса = Модель.НайтиПоИдентификатору(ТекущийВопросИндентификатор);
	СтрокаВопроса.ОтветВыборПользователя = Истина; // заполняем ОтветВыборПользователя
	
	// получили текущую строку модели
	// ТекущийВопросИдентификатор - идентификатор текущего вопроса
	ТекущаяСтрокаМодели = Модель.НайтиПоИдентификатору(ТекущийВопросИндентификатор);
	// Получаем ответы текущего вопроса
	ЭлементыСтроки = ТекущаяСтрокаМодели.ПолучитьЭлементы();
	Индекс = 0;
	
	// заполняем ответы пользователя
	Для каждого Элемент Из ЭлементыСтроки Цикл
	
		Если МассивИндексовОтветов.Найти(Индекс) = Неопределено Тогда
			Элемент.ОтветВыборПользователя = Ложь;
		иначе
			Элемент.ОтветВыборПользователя = Истина;
		КонецЕсли;
		
		Индекс = Индекс + 1;
	КонецЦикла;
	
	// Если на вопрос дан ответ то удалить этот вопрос из списка вопросов
	Для каждого ЭлементСпискаВопросов Из СписокВопросов Цикл
	
		Если ЭлементСпискаВопросов.Значение = ТекущийВопросИндентификатор Тогда
			СписокВопросов.Удалить(ЭлементСпискаВопросов);
			Прервать;
		КонецЕсли;
	
	КонецЦикла;
	
	// Если в списке вопросов еще есть вопросы позиционируемся на следующем вопросе
	Если СписокВопросов.Количество() > 0 Тогда
		ТекущийВопросИндентификатор = СписокВопросов[0].ПолучитьИдентификатор(); // записали идентификатор следующего вопроса
		Элементы.СписокВопросов.ТекущаяСтрока = ТекущийВопросИндентификатор; // присвоили текущей строке полученный идентификатор
		
		// выводим ответы вопроса на котором спозиционировались
		ЗаполнитьОтветыВопроса(ТекущийВопросИндентификатор);
	Иначе 
		// вопросы закончились
		СписокОтветовВопроса.Очистить();
		// добавляем новое состояние процесса
		СостояниеПроцесса = "ПередЗакончитьТестирование";
		УстановитьВидимостьДоступность();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьТестирование(Команда)
	СостояниеПроцесса = "ЗакончитьТестирование";
	
	// Проверим, есть ли вопросы без ответов
	КоличествоВопросовБезОтвета = КоличествоВопросовБезОтвета();
	
	Если КоличествоВопросовБезОтвета > 0 Тогда  // остались вопросы без ответа
		Cообщение = Новый СообщениеПользователю; // сообщим об этом пользователю
		Cообщение.ПутьКДанным = "СписокВопросов";
		ФорматнаяСтрока = ";%1 вопрос;;%1 вопроса;%1 вопросов;%1 вопроса";
		Cообщение.Текст = СтрШаблон("Вы еще не ответили на %1!", СтрокаСЧислом(
			// параметр содержит перечисленные через 
			// точку с запятой 6 форм строки 
			// для каждой категории числительного:
			// Ноль, Один, Два, Немного, Много, Другое       
			// Для русского (ru), белорусского (be) и украинского (uk) языков: 
			//"; день; ; дня; дней; дня
			ФорматнаяСтрока,
			КоличествоВопросовБезОтвета,
			ВидЧисловогоЗначения.Количественное));
		Cообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	// заполняем параметры модели по результатам ответов на вопросы
	РассчитатьМодель();
	
	// добавим диалог сохранения результата в xml файл
	ТекстВопроса = "Спасибо.Тестирование завершено. Сохранить результат в xml?";
	Обработчик = Новый ОписаниеОповещения("ЗакончитьТестированиеЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	СостояниеПроцесса = "Распечатан";
	УстановитьВидимостьДоступность();
	// выводим табличный документ результата теста
	ТабДок = ПечатьНаСервере();
	
	//установим параметры отображения табличного документа
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДОк.Показать("Показать результаты тестирования");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	 // Настройка формы в зависимости от состояния процесса
	Если СостояниеПроцесса = "Начало" Тогда
		Элементы.ФормаЗакончитьТестирование.Доступность = Ложь;
		Элементы.ФормаПечать.Доступность = Ложь;
	ИначеЕсли СостояниеПроцесса = "НачатьТестирование" Тогда
		Элементы.ФормаНачатьТестирование.Доступность = Ложь;
		Элементы.ФормаЗакончитьТестирование.Доступность = Истина;
		Элементы.Ответить.КнопкаПоУмолчанию = Истина;
	ИначеЕсли СостояниеПроцесса = "ЗакончитьТестирование" Тогда
		Элементы.ФормаЗакончитьТестирование.Доступность = Ложь;
		Элементы.ФормаПечать.Доступность = Истина;
		Элементы.ФормаНачатьТестирование.Доступность = Ложь;
		Элементы.Ответить.Доступность = Ложь;
		Элементы.ФормаПечать.КнопкаПоУмолчанию = Истина;
	ИначеЕсли СостояниеПроцесса = "Распечатан" Тогда 
		Элементы.ФормаЗакончитьТестирование.Доступность = Ложь;
		Элементы.ФормаПечать.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#Область ЗаполнениеМодели

&НаСервере
Процедура ЗаполнитьМодельКодом()
	// Модель заполняем вручную
	// тестовые данные надо подобрать так, чтобы отработать все варианты
	// МножественныйВыбор - Истина
	// - РежимТрактовки - Истина
	// - РежимТрактовки - Ложь    
	// МножественныйВыбор - Ложь
	
	Дерево = РеквизитФормыВЗначение("Модель"); // получаем наше ДеревоЗначений
	// Заполняем ДеревоЗначений вопросами и ответами
	// Вопрос1
	Вопрос1 = Дерево.Строки.Добавить();
	Вопрос1.Текст = "Столица России?";
	вопрос1.РежимТрактовки = Истина;
	Вопрос1.МножественныйВыбор = Ложь;
	
	// Ответы к вопросу добавляем как подстроки к вопросу
	Ответ0 = Вопрос1.Строки.Добавить();
	Ответ0.Текст = "Париж";
	Ответ0.ОтветВерный = Ложь;
	Ответ0.ОтветВыборПользователя = Ложь;
	
	Ответ1 = Вопрос1.Строки.Добавить();
	Ответ1.Текст = "Рим";
	Ответ1.ОтветВерный = Ложь;
	Ответ1.ОтветВыборПользователя = Ложь;
	
	Ответ2 = Вопрос1.Строки.Добавить();
	Ответ2.Текст = "Москва";
	Ответ2.ОтветВерный = Истина;
	Ответ2.ОтветВыборПользователя = Ложь;
	
	//Вопрос2
	Вопрос2 = Дерево.Строки.Добавить();
	Вопрос2.Текст = "Куда указывает стрелка компаса?";
	Вопрос2.РежимТрактовки = Истина;
	Вопрос2.МножественныйВыбор = Истина;
	
	// Ответы для второго вопроса создаем в подстроках для вопроса2
	Ответ0 = Вопрос2.Строки.Добавить();
	Ответ0.Текст = "Юг";
	Ответ0.ОтветВерный = Ложь;
	Ответ0.ОтветВыборПользователя = Ложь;
	
	Ответ1 = Вопрос2.Строки.Добавить();
	Ответ1.Текст = "Запад";
	Ответ1.ОтветВерный = Ложь;
	Ответ1.ОтветВыборПользователя = Ложь;
	
	Ответ2 = Вопрос2.Строки.Добавить();
	Ответ2.Текст = "Север";
	Ответ2.ОтветВерный = Истина;
	Ответ2.ОтветВыборПользователя = Ложь;
	
	// Вопрос3
	Вопрос3 = Дерево.Строки.Добавить();
	Вопрос3.Текст = "Сколько пальцев на руке?";
	Вопрос3.МножественныйВыбор = Ложь;
	Вопрос3.МножественныйВыбор = Истина;
	
	// Ответы
	Ответ0 = Вопрос3.Строки.Добавить();
	Ответ0.Текст = "Меньше пяти";
	Ответ0.ОтветВерный = Ложь;
	Ответ0.ОтветВыборПользователя = Ложь;
	
	Ответ1 = Вопрос3.СТроки.Добавить();
	Ответ1.Текст = "Пять";
	Ответ1.ОтветВерный = Истина;
	Ответ1.ОтветВыборПользователя = Ложь;
	
	Ответ2 = Вопрос3.СТроки.Добавить();
	Ответ2.Текст = "Меньше десяти";
	Ответ2.ОтветВерный = Истина;
	Ответ2.ОтветВыборПользователя = Ложь;
	
	// Возвращаем данные на форму
	ЗначениеВРеквизитФормы(Дерево, "Модель");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМодель(МассивВопросов)
	ДеревоЗначений = РеквизитФормыВЗначение("Модель");
	
	// Заполняем дерево значений
	Для каждого Элемент Из МассивВопросов Цикл
		СтрокаВопрос = ДеревоЗначений.Строки.Добавить();
		СтрокаВопрос.Текст = Элемент["Текст"]; // получаем свойство ТЕкст из соответствия
		СтрокаВопрос.РежимТрактовки = Элемент["РежимТрактовки"]; // // получаем свойство РежимТрактовки из соответствия
		СтрокаВопрос.МножественныйВыбор = Элемент["МножественныйВыбор"];// получаем свойство МножественныйВыбор из соответствия
		
		// Получаем массив с ответами на текущий вопрос
		Ответы = Элемент["ВариантыОтветов"];
		
		// Заполняем ответы
		Для каждого ВариантОтвета Из Ответы Цикл
			СтрокаОтвет = СтрокаВопрос.Строки.Добавить(); // добавляем подстроку к строке ответа
			СтрокаОтвет.Текст = ВариантОтвета["Текст"]; // заполняем свойства ответов
			СтрокаОтвет.ОтветВерный = ВариантОтвета["ОтветВерный"]; 
		КонецЦикла;
		
	КонецЦикла;
	// Возвращаем данные в реквизит формы
	ЗначениеВРеквизитФормы(ДеревоЗначений, "Модель");
КонецПроцедуры

&НаСервере
Процедура ЧтениеJsonЧерезВременныйФайлНаСервере()
	// Прочитаем двоичные данные из макета, где записан файл Json с вопросами и ответами для теста
	// Используем временный файл
	
	// Получим общий макет с данными
	МакетДД = ПолучитьОбщийМакет("Json");
	// Получаем имя временного файла
	ИмяФайла = ПолучитьИмяВременногоФайла("Json");
	// Двоичные данные из макета записываем в файл
	МакетДД.Записать(ИмяФайла);
	// Создаем объект ЧтениеJson
	ЧтениеJson = Новый ЧтениеJSON;
	// Открываем файл с двоичными данными для чтения объектом ЧтениеJSON
	ЧтениеJson.ОткрытьФайл(ИмяФайла);
	// Читаем данные из файла. Результат чтения будет массив, элементы массива - соответствие
	// Свойства соответствия - вопросы и вложенный массив соответствий с ответами
	Значение = ПрочитатьJSON(ЧтениеJson, Истина, "updated_at", ФорматДатыJSON.Microsoft);
	// Закрыть объект ЧтениеJson
	ЧтениеJson.Закрыть();
	// Заполнить модель данными из массива
	ЗаполнитьМодель(Значение);
КонецПроцедуры

&НаСервере
Процедура ЧтениеJsonЧерезПотокВПамятиНаСервере()
	// Прочитаем двоичные данные из макета, где записан файл Json с вопросами и ответами для теста
	// Получим общий макет с данными
	МакетДД = ПолучитьОбщийМакет("Json");
	// Создаем объект ПотокВПамяти
	ПотокДанных = Новый ПотокВПамяти;
	// Создаем объект ЗаписьДанных
	Запись = Новый ЗаписьДанных(ПотокДанных);
	// Записываем двоичные данные в поток
	Запись.Записать(МакетДД);
	// Устанавливаем начальную позицию в потоке
	ПотокДанных.Перейти(0, ПозицияВПотоке.Начало);
	// Закрываем Запись
	Запись.Закрыть();
	// Создаем объект ЧтениеJson
	ЧтениеJson = Новый ЧтениеJSON;
	// Открываем поток для чтения
	ЧтениеJson.ОткрытьПоток(ПотокДанных, "КодировкаТекста.UTF8");
	// Получаем данные из потока
	Значение = ПрочитатьJSON(ЧтениеJson, ИСтина, "updated_at", ФорматДатыJSON.Microsoft);
	// Закрываем объект ЧтениеJson
	ЧтениеJson.Закрыть();
	// Заполняем модель
	ЗаполнитьМодель(Значение);
КонецПроцедуры

&НаСервере
Процедура ЧтениеJsonЧерезСтрокуИзДвоичныхДанныхСервера()
	// Получаем общий макет
	МакетДД = ПолучитьОбщийМакет("Json");
	// Получаем строку из двоичных данных нашего макета
	Текст = ПолучитьСтрокуИзДвоичныхДанных(МакетДД, КодировкаТекста.UTF8);
	// Создаем объект ЧтениеJson
	ЧтениеJson = Новый ЧтениеJSON;
	// установим строку для чтения
	ЧтениеJson.УстановитьСтроку(Текст);
	// Читаем данные из строки в массив соответствий
	Значение = ПрочитатьJSON(ЧтениеJson, ИСтина, "updated_at", ФорматДатыJSON.Microsoft);
	ЧтениеJson.Закрыть();
	// Заполняем модель
	ЗаполнитьМодель(Значение);
КонецПроцедуры

&НаСервере
Процедура ЧтениеJsonЧерезОбъектЧтениеДанныхНаСервере()
	// Получаем общий макет
	МакетДД = ПолучитьОбщийМакет("Json");
	// Создаем объект ЧтениеДанных
	ЧтениеДанных = Новый ЧтениеДанных(МакетДД, КодировкаТекста.UTF8);
	// Читаем символы из двоичных данных
	Текст = ЧтениеДанных.ПрочитатьСимволы();
	// Закрываем объект ЧтениеДАнных
	ЧтениеДанных.Закрыть();
	// Создаем объект ЧтениеJson
	ЧтениеJson = Новый ЧтениеJSON;
	// устанавливаем строку для чтения
	ЧтениеJson.УстановитьСтроку(Текст);
	// считываем данные в массив соответствий
	Значение = ПрочитатьJSON(ЧтениеJson, ИСтина, "updated_at", ФорматДатыJSON.Microsoft);
	ЧтениеJson.Закрыть();
	// Заполняем модель
	ЗаполнитьМодель(Значение);
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ЗакончитьТестированиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	// если не выбран Да то заканчиваем
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	// иначе запускаем процедуру записи в xml файл
	// создаем объект ДиалогВыбораФайла
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Заголовок = "Выберите каталог для сохранения файла";
	Диалог.Показать(Новый ОписаниеОповещения("ЗаписатьМодельТестаВXMLЗавершение", ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьМодельТестаВXMLЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт 
	// если каталог для записи не выбран то выход
	Если ВыбранныеФайлы = Неопределено Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Вы не сохранили результат в файл XML!";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	// записываем результат в Xml и получаем его двоичные данные 
	ДД = ДвоичныеДанныеРезультата();
	
	// для линукс раздилитель в имени файла / для windows - \  
	Разделитель = ЭтоЛинукс(); 
	// формируем имя файла
	ИмяФайла = ВыбранныеФайлы[0] + Разделитель + "РезультатТестирования.xml";
	// Сохраним данные в файл
	ДД.Записать(ИмяФайла); 
	
	// Откроем сохраненный файл в приложении
	Обработчик = Новый ОписаниеОповещения("НачатьЗапускПриложенияЗавершение", ЭтаФорма);
	НачатьЗапускПриложения(Обработчик, ИмяФайла); 
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗапускПриложенияЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт 
	// пустой обработчик
КонецПроцедуры

&НаСервере
Функция ДвоичныеДанныеРезультата()
	// создаем объект ЗаписьXML
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписьXML.ЗаписатьНачалоЭлемента("ВопросыТеста"); // запись начала элемента
	
	НомерВопроса = 0;
	// Получаем ДеревоЗначений модели
	ДеревоЗначений = РеквизитФормыВЗначение("Модель");
	
	Для каждого ТекущийВопрос Из ДеревоЗначений.Строки Цикл
		НомерВопроса = НомерВопроса + 1; // увеличиваем номер вопроса
		ЗаписьXML.ЗаписатьНачалоЭлемента("Вопрос" + XMLСтрока(НомерВопроса)); // начало элемента вопрос
		// записываем атрибуты
			ЗаписьXML.ЗаписатьАтрибут("МножественныйВыбор", XMLСтрока(ТекущийВопрос.МножественныйВыбор));
			ЗаписьXML.ЗаписатьАтрибут("РежимТрактовки", XMLСтрока(ТекущийВопрос.РежимТрактовки));
			ЗаписьXML.ЗаписатьАтрибут("ОтвеченоВерно", XMLСтрока(ТекущийВопрос.ОтвеченоВерно));
			
			ЗаписьXML.ЗаписатьНачалоЭлемента("ТекстВопроса"); // запись самого текста вопроса
				ЗаписьXML.ЗаписатьТекст(XMLСтрока(ТекущийВопрос.Текст));
			ЗаписьXML.ЗаписатьКонецЭлемента(); // закрытие элемента ТекстВопроса
			
			// записываем ответы 
			ЗаписьXML.ЗаписатьНачалоЭлемента("Ответы"); // начальный элемент Ответы
			Для каждого ТекущийОтвет Из ТекущийВопрос.Строки Цикл
				ЗаписьXML.ЗаписатьНачалоЭлемента("Ответ"); // начальный элемент Ответ
					ЗаписьXML.ЗаписатьАтрибут("ОтветВерный", XMLСтрока(ТекущийОтвет.ОтветВерный));
					ЗаписьXML.ЗаписатьАтрибут("ОтветВыборПользователя", XMLСтрока(ТекущийОтвет.ОтветВыборПользователя));
					ЗаписьXML.ЗаписатьТекст(XMLСтрока(ТекущийОтвет.Текст)); // текст ответа
				ЗаписьXML.ЗаписатьКонецЭлемента(); // конец элемента ответ
			КонецЦикла;
			ЗаписьXML.ЗаписатьКонецЭлемента(); // конец элемента Ответы
		ЗаписьXML.ЗаписатьКонецЭлемента(); // конец элемента Вопрос
	КонецЦикла; 
	
	ЗаписьXML.ЗаписатьКонецЭлемента(); // конец элемента ВопросыТеста
	СтрокаXML = ЗаписьXML.Закрыть(); // Завершение записи XML
	
	Возврат ПолучитьДвоичныеДанныеИзСтроки(СтрокаXML, КодировкаТекста.UTF8); // возвращаем двоичные данные
КонецФункции

&НаКлиенте
Процедура ЗаполнитьОтветыВопроса(ВыбраннаяСтрока)
	// В выбранной строке получаем число,которое является идентификатором вопроса в нашей модели. 
	// Его мы записали в параметр Значение в СписокВопросов при выводе вопросов
	СтрокаСписка = СписокВопросов.НайтиПоИдентификатору(ВыбраннаяСтрока); // по идентификатору нашли вопрос выбранный на форме в СписокВопросов
	
	// Найдем вопрос в нашей модели
	СтрокаВопроса = Модель.НайтиПоИдентификатору(СтрокаСписка.Значение);
	// Записываем Идентификатор вопроса в реквизит формы
	ТекущийВопросИндентификатор = СтрокаВопроса.ПолучитьИдентификатор();
	// Записываем вопрос в реквизит формы
	ТекущийВопросТекст = СтрокаВопроса.Текст;
	СписокОтветовВопроса.Очистить();
	
	// Заполняем ответами СписокОтветовВопроса
	Для каждого СтрокаОтвета Из СтрокаВопроса.ПолучитьЭлементы() Цикл
		СписокОтветовВопроса.Добавить(СтрокаОтвета.ПолучитьИдентификатор(), СтрокаОтвета.Текст);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция КоличествоВопросовБезОтвета()
	Перем КоличествоВопросовБезОтвета, СтрокаМодели;
	КоличествоВопросовБезОтвета = 0;
	
	Для каждого СтрокаМодели Из Модель.ПолучитьЭлементы() Цикл
		Если СтрокаМодели.ОтветВыборПользователя Тогда
			Продолжить;
		КонецЕсли;
		КоличествоВопросовБезОтвета = КоличествоВопросовБезОтвета + 1;
	КонецЦикла;
	
	Возврат КоличествоВопросовБезОтвета;
КонецФункции

&НаКлиенте
Процедура РассчитатьМодель()
	Перем ВопросМодели, ОтветНавопрос;
	// обходим модель
	
	// РежимТрактовки - истина, если правильным считается "хотя бы один верный ответ"
	// ложь - если правильным считается ответ со всеми выбранными корректными вариантами
	// Заполняем параметр модели ОтвеченоВерно
	Для каждого ВопросМодели Из Модель.ПолучитьЭлементы() Цикл
		Для каждого ОтветНаВопрос Из ВопросМодели.ПолучитьЭлементы() Цикл
			// заполняем ОтвеченоВерно
			Если ОтветНаВопрос.ОтветВерный = ОтветНаВопрос.ОтветВыборПользователя И ОтветНаВопрос.ОтветВерный = Истина Тогда
				ВопросМодели.ОтвеченоВерно = Истина;
			КонецЕсли;
			
			// Учитываем МножественныйВыбор и РежимТрактовки
			Если ВопросМодели.МножественныйВыбор = Истина И ВопросМодели.РежимТрактовки = Ложь Тогда
			
				Если ОтветНаВопрос.ОтветВерный = Истина И ОтветНаВопрос.ОтветВыборПользователя = Ложь Тогда
					ВопросМодели.ОтвеченоВерно = Ложь;
					Продолжить;
				КонецЕсли;
			
			КонецЕсли;
		
		КонецЦикла;
		
	КонецЦикла;
КонецПроцедуры 

&НаСервере
функция ПечатьНаСервере()
	ТабДок = Новый ТабличныйДокумент; // создали табличный документ
	Макет = Обработки.Обработка1.ПолучитьМакет("Макет"); // получили макет таблицы, который создали
	
	// Выводим шапку
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ТабДок.Вывести(Шапка);
	
	// Получаем область строки табличной части
	ТабличнаяЧастьСтрока = Макет.ПолучитьОбласть("Строка");
	// Получаем параметры области
	ПараметрыСтроки = ТабличнаяЧастьСтрока.Параметры;
	ПараметрыСтроки.НомерСтроки = 0;
	
	// создадим временные переменные для расчеты количества правильных вопросов и сколько всего вопросов
	КоличествоВсегоВопросов = 0;
	КоличествоПравильныхВопросов = 0;
	
	// Получим ДеревоЗначений нашей Модели
	МодельДерево = РеквизитФормыВЗначение("Модель");
	
	// Обходим дерево и получаем данные из модели для вывода в таблицу
	Для каждого ВопросМодели Из МодельДерево.Строки Цикл
		КоличествоВсегоВопросов = КоличествоВсегоВопросов + 1; // счетчик вопросов увеличиваем
		// Если на вопрос ответ верный увеличиваем счетчик правильных ответов
		Если ВопросМодели.ОтвеченоВерно Тогда
			КоличествоПравильныхВопросов = КоличествоПравильныхВопросов + 1;
		КонецЕсли;
		
		// Выводим параметры вопросов
		ПараметрыСтроки.ВопросТеста = СокрЛП(ВопросМодели.Текст); // получаем текст вопроса
		ПараметрыСтроки.РежимПроверки = ВопросМодели.МножественныйВыбор;
		ПараметрыСтроки.ОтвеченоВерно = ВопросМодели.ОтвеченоВерно;
		
		// Создадим массивы для правильных ответов и для ответов пользователя 
		// Далее выведем содержимое массив в ячейку таблицы
		ОтветыПользователя = Новый Массив;
		ПравильныеОтветы = Новый Массив;
		
		// Заполним массивы
		Для каждого ОтветНаВопрос Из ВопросМодели.Строки Цикл
			Если ОтветНаВопрос.ОтветВыборПользователя Тогда
				// Если пользователь выбрал текущий ответ, добавим его в массив
				ОтветыПользователя.Добавить(ОтветНаВопрос.Текст);
			КонецЕсли;
			
			Если ОтветНаВопрос.ОтветВерный Тогда
			// Если ответ правильный то добавляем в массив
				ПравильныеОтветы.Добавить(ОтветНаВопрос.Текст);
			КонецЕсли;
		КонецЦикла;
		
		// содержимое массивов выведем в ячейки таблицы
		ПараметрыСтроки.ПравильныеОтветы = СтрСоединить(ПравильныеОтветы, ",");
		ПараметрыСтроки.ОтветыПользователя = СтрСоединить(ОтветыПользователя, ",");
		// Увеличим номер строки
		ПараметрыСтроки.НомерСтроки = ПараметрыСтроки.НомерСтроки + 1;
		
		// Выведем заполненную строку таблицы
	ТабДок.Вывести(ТабличнаяЧастьСтрока);
	КонецЦикла; 
	
	// Выведем Итоги
	Итого = Макет.ПолучитьОбласть("Итого"); // получим именованную ячейку
	// получим параметры итогов
	ПараметрыИтого = Итого.Параметры;
	// Заполним параметры
	ПараметрыИтого.КолПро = КоличествоПравильныхВопросов;
	ПараметрыИтого.КолВсего = КоличествоВсегоВопросов;
	
	// Выводим итоги
	ТабДок.Вывести(Итого);
	Возврат ТабДок;

КонецФункции

&НаКлиенте
Функция ЭтоЛинукс()
	Перем Разделитель, СистемнаяИнформация, ЭтоЛинукс;
	
	// Содержит описание технических характеристик компьютера.
	СистемнаяИнформация = Новый СистемнаяИнформация;
	// проверка операционной системы. Истина если будет Линукс
	ЭтоЛинукс = СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86 Или СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86_64;
	// если Линукс разделитель /
	Разделитель = ?(ЭтоЛинукс, "/", "\");
	Возврат Разделитель; 
КонецФункции
#КонецОбласти

#Область Инициализация

#КонецОбласти